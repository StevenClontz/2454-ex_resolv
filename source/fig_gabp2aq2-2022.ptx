<?xml version='1.0' encoding='utf-8'?>
<figure xml:id="fig_gabp2aq2-2022">
    <caption>Superfícies da <xref ref="p2q2a-2022" /></caption>
    <image width="50%" xml:id="gabp2aq2-2022" archive="html">
    <description>Interseção de superfićies e seu vetor tangente.</description>
    <asymptote>
      import solids;
      size(10cm);
      
      currentprojection=orthographic(3,-2,1);
      limits((-2,-2,-2),(2,2,2));
      
      currentlight=light(white,(2,2,2),(2,-2,-2));
      // uncomment for transparent background
      // currentlight.background=black+opacity(0);

      path3 hipxz=graph(new triple(real t) {
      return (sqrt(1+t*t),0,t);},-2,2,20,operator ..);

      revolution hip1frame=revolution(hipxz,axis=Z);
      surface hiperboloide1f=surface(hip1frame);

      surface esfera=shift(1,1,0)*unitsphere;

      path3 curva=graph(new triple(real t) {
      return (1/2*(sqrt(2)*cos(t)+1),1/2*(sqrt(2)*sin(t)+1),1/2^(1/4)*sqrt(cos(t)+sin(t)));},pi/4,3*pi/4,20,operator ..);

      draw(hiperboloide1f, lightblue);
      draw(esfera, orange+opacity(0.8));
      draw(curva,red+1bp);
      draw(reflect(O,X+Y,Z)*curva,red+1bp);
      draw(reflect(O,X,Y)*curva,red+1bp);
      draw(rotate(180,X+Y)*curva,red+1bp);

      triple ponto=(1,1,1);

      dot(ponto);
      draw(Label("$\nabla g(1,1,1)$",position=EndPoint),
      ponto--ponto+(2,2,-2),lightblue,Arrow3);
      draw(Label("$\nabla h(1,1,1)$",position=EndPoint),
      ponto--ponto+(0,0,2),orange,Arrow3);
      draw(Label("$\vec{u_1}$",position=EndPoint),
      ponto--ponto+1/sqrt(2)*(1,-1,0),black,Arrow3);

      xaxis3("$x$",Arrow3); 
      yaxis3("$y$",Arrow3);
      zaxis3("$z$",Arrow3);
    </asymptote>
    </image>
</figure>
